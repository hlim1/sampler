from SCons.Errors import StopError

Import('env')

env = env.Clone(
    parse_flags='!llvm-config --cxxflags --ldflags',
    tools=('llvm',),
    toolpath='.',
    )

llvm_version = env['LLVM_version']
llvm_require = '3.0'
if llvm_version < '3.0':
    raise StopError('LLVM %s too old; need at least version %s' % (llvm_version, llvm_require))

env.AppendUnique(
    CXXFLAGS='-std=c++11',
    INCPREFIX='-isystem ',
    LIBS='LLVM-$LLVM_version',
    SHLINKFLAGS='-Wl,--no-undefined',
    )

if env['domainname'] == 'cs.wisc.edu':
    env.AppendUnique(CPPPATH='/unsup/boost-1.49.0/include')

instrumentor = env.SharedLibrary(
    'Instrumentor', (
        'Branches.cc',
        'Dummy.cc',
        'Returns.cc',
        'Sampler.cc',
        'SchemeBase.cc',
        'SitesRegistry.cc',
        ),
    )

removeDummyUses = env.SharedLibrary(
    'RemoveDummyUses.cc',
    )

installed = Install('#llvm-driver', (
        instrumentor,
        removeDummyUses,
        ))
Default(installed)
