from glob import glob
from itertools import chain, ifilter, imap
import re


Import('env', 'excludedSources')
env = env.Clone(OCAML_LIBS=['unix', 'str', 'cil'], OCAML_WARN='Ael',
                tools=['expect', 'test'], toolpath=['.'])
env.AppendUnique(OCAML_PATH=['instrumentor', 'ocaml', '$cil_path'])

env.File('test.py')


########################################################################
#
#  create OCaml source defining sampler version number
#


[version_ml] = env.Template('version.ml.in', varlist=['VERSION'])


########################################################################
#
#  create OCaml source listing standard library functions
#


symbolPattern = re.compile('^[0-9a-f]{8} [TW] ([$\\w]+)')


def findSymbols(library):
    command = ['nm', '--defined-only', '-g', '-p', str(library)]
    if library.get_suffix() != '.a':
        command.append('-D')

    nm = env.ReadPipe(command)
    matches = imap(symbolPattern.match, nm)
    matches = ifilter(None, matches)
    return ( match.group(1) for match in matches )


def libfuncs(target, source, env):
    libraries = source[1:]
    symbols = chain(*imap(findSymbols, libraries))
    symbols = ['"%s"' % symbol for symbol in symbols ]

    env.Instantiate(
        source[0], target[0],
        LIBRARY_FUNCTIONS=';\n'.join(symbols),
        LIBRARY_COUNT=len(symbols),
        )


syslibs = ['/usr/lib/libc_nonshared.a', '/lib/libc.so.6', '/lib/libm.so.6']
excludedSources |= set(syslibs)
[libraries_ml] = env.Command('libraries.ml', ['libraries.ml.in', syslibs], libfuncs)


########################################################################
#
#  main build targets
#


sources = [version_ml, libraries_ml] + glob('*.ml') + glob('*.mli')
env.OcamlObject(sources)

main = env.OcamlProgram('main.ml')
misc = map(env.OcamlProgram, [
    'cfgTest.ml',
    'cfgToDot.ml',
    'checker.ml',
    'dotify.ml',
    'dump.ml',
    'dumpConstants.ml',
    'loopless.ml',
])
Default(misc)

Default(Install('#driver', main))

Alias('install', env.Install('$DESTDIR$driverdir', main))


########################################################################
#
#  source distribution
#


sources = set(sources)
for generated in ['libraries.ml', 'version.ml']:
    sources.discard(generated)
    sources.add(generated + '.in')


#########################################################################
#
#  regression test subdirs
#

SConscript(dirs=[
    'balance',
    'bounds',
    'branches',
    'function-entries',
    'g-object-unref',
    'nothing',
    'returns',
    'scalar-pairs',
    'timestamps',
    ],
           exports='env')
