#!/usr/bin/perl -wT

use strict;

use CGI qw(:standard -private_tempfiles);
use CGI::Carp qw(fatalsToBrowser);
use CGI::Pretty;

use File::Copy;
use FileHandle;

use POSIX qw(strftime);


########################################################################


my $query = new CGI;

exists $ENV{UNIQUE_ID} or die "\$UNIQUE_ID missing from environment\n";
$ENV{UNIQUE_ID} =~ /^([-0-9A-Za-z@]+)$/ or die "\$UNIQUE_ID looks suspicious: \"$ENV{UNIQUE_ID}\"\n";
my $uid = $1;
print($query->header({-sampler_unique_id => $uid}));

umask 0007;

my $dir = "/var/www/sampler-uploads/$uid";
mkdir $dir or die "cannot build upload directory $dir: $!\n";
END { rmdir $dir if $dir; }


sub stash_environ () {
    my $environment = new FileHandle "$dir/environment", 'w' or die $!;
    local ($,, $\) = ("\t", "\n");
    while (my ($key, $value) = each %ENV) {
	$environment->print($key, $value)
	    if $key =~ /^HTTP_SAMPLER_/;
    }
    $environment->print('DATE', strftime('%F %T', gmtime));
}


sub stash_file ($) {
    my $name = shift;
    $name =~ /^([-_0-9A-Za-z]+)$/ or die "uploaded file name looks suspicious: $name\n";
    my $filename = $1;
    my $in = $query->upload($name) or die "cannot read uploaded file: $!\n";
    my $out = new FileHandle "$dir/$filename.gz", 'w' or die "cannot stash uploaded file: $!\n";
    copy($in, $out);
}


stash_environ();
stash_file $_ foreach $query->param;


########################################################################


use lib '/usr/lib/perl5/site_perl/5.6.1';
use DB_File::Lock;
use POSIX qw(S_IRUSR S_IWUSR);


my $remote = $query->remote_host;

my %survey;
tie %survey, 'DB_File::Lock', '/var/www/sampler-survey.db', O_RDONLY, 0, $DB_HASH, 'read';
my $novel = not exists $survey{$remote};
untie %survey;


if ($novel) {
    print($query->start_html('Distribution Change Survey'),
	  $query->h1('Distribution Change Survey'),
	  $query->p('Thank you for participating in the Cooperative Bug Isolation Project.  We are considering changing our CBI build platform and would like your input.'),
	  $query->p('Red Hat Linux 9 officially reaches end-of-life on April 30, 2004.  Red Hat will no longer release errata or security updates for this distribution.  For this reason, we are considering changing our CBI build platform to Fedora Core 1.  The existing Red Hat Linux 9 packages would still be available, but future builds and new releases would be for Fedora Core 1.'),
	  $query->p('If we made this change to Fedora Core 1, would you still be able to use our packages?  Would you be able to use them on more machines than you use now?  Please let us know:'),
	  $query->ul($query->li($query->a({href => 'http://sampler.cs.berkeley.edu/cgi-bin/sampler-survey?distro=fedora-core-1'},
					  'Yes, switch to Fedora Core 1.  I will be able to use new packages on as many or more computers than I do now.')),
		     $query->li($query->a({href => 'http://sampler.cs.berkeley.edu/cgi-bin/sampler-survey?distro=either'},
					  'Neutral.  My use of CBI packages would remain about the same either way.')),
		     $query->li($query->a({href => 'http://sampler.cs.berkeley.edu/cgi-bin/sampler-survey?distro=redhat-linux-9'},
					  'No, stay with Red Hat Linux 9.  I am not using Fedora Core 1 and have no plans to use it any time soon.'))),
	  $query->end_html);
}
