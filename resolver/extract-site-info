#!/usr/bin/perl -w

use strict;
use 5.008;			# for safe pipe opens using list form of open

use Fcntl qw(SEEK_SET);
use FileHandle;


########################################################################


die "Usage: $0 [<executable> | <object> | <library>] ...\n" unless @ARGV >= 1;

foreach my $name (@ARGV) {
    my $objdump = new FileHandle;
    open $objdump, '-|', 'objdump', '-h', '-w', $name
	or die "cannot run objdump: $!\n";

    my $size;
    my $offset;
    while (<$objdump>) {
	my @field = split;
	next unless @field >= 7;
	next unless $field[1] eq '.debug_site_info';
	next unless $field[0] =~ /^\d+$/;
	$size = hex $field[2];
	$offset = hex $field[5];
	print ".debug_site_info: offset $offset (0x$field[5]), size $size (0x$field[2])\n";
	last;
    }

    die "\".debug_site_info\" section is missing\n" unless defined $offset;
    die "\".debug_site_info\" section is empty\n" unless $size;

    my $buffer;
    my $executable = new FileHandle $name, 'r';
    $executable->seek($offset, &SEEK_SET);
    $executable->read($buffer, $size);
    $buffer =~ s/\0//g;
    print $buffer;
}
