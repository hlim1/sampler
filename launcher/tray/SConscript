Import('env', 'testPythons')
env = env.Clone(bindir=env.subst('$bindir'),
               traydir=env.subst('$traydir'))

service = env.Template('edu.wisc.cs.cbi.Monitor.service.in', varlist=['traydir'])
paths = env.Template('Paths.py.in', varlist=['bindir', 'traydir'])
tray = env.Template('tray.in', varlist=['commondir', 'traydir'])
ui = env.Template('tray.ui.in', varlist=['version'])
Default(service, paths, tray, ui)

pythons = [
    'AboutBoxIcon.py',
    'Main.py',
    'Paths.py',
    'Service.py',
    'TrayIcon.py',
    ]

compiled = env.PythonBytecodeOpt(pythons)
Default(compiled)

testPythons(env, pythons)
Alias('test', [
        env.TestXML(ui, schema=File('../gtk-builder.xsd')),
        ])

Alias('install', [
    env.Install('$DESTDIR$servicesdir', service),
    env.Install('$DESTDIR$traydir', [compiled, tray, ui]),
    ])
