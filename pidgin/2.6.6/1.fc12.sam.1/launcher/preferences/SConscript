Import('env', 'testPythons')
env = env.Clone(preferencesdir=env.subst('$preferencesdir'))

Default(env.Template('Paths.py.in', varlist=['preferencesdir']))
pythons = ['AppFinder.py', 'Application.py', 'AppModel.py', 'Main.py', 'PreferencesDialog.py', 'Service.py']

pythons.append('Paths.py')
compiled = env.PythonBytecodeOpt(pythons)
Default(compiled)

prefs = env.Template('sampler-preferences.in', varlist=['preferencesdir', 'commondir'])
Default(prefs)

desktop = env.File('sampler-preferences.desktop')

testPythons(env, pythons)
Alias('test', [
    env.TestDesktop(desktop),
    env.TestXML('preferences.ui', schema=File('../gtk-builder.xsd')),
    ])

Alias('install', [
    env.Install('$DESTDIR$applicationsdir', desktop),
    env.Install('$DESTDIR$bindir', prefs),
    env.Install('$DESTDIR$preferencesdir', [compiled, 'preferences.ui']),
    ])
