#!/usr/bin/perl -wT

use strict;

use CGI qw(:standard -private_tempfiles);
use CGI::Carp qw(fatalsToBrowser);

use File::Copy;
use FileHandle;

use POSIX qw(strftime);


########################################################################


my $query = new CGI;

exists $ENV{UNIQUE_ID} or die "\$UNIQUE_ID missing from environment\n";
$ENV{UNIQUE_ID} =~ /^([-0-9A-Za-z@]+)$/ or die "\$UNIQUE_ID looks suspicious: \"$ENV{UNIQUE_ID}\"\n";
my $uid = $1;
print($query->header({-sampler_unique_id => $uid}));

umask 0007;

my $dir = "/var/www/sampler-uploads/$uid";
mkdir $dir or die "cannot build upload directory $dir: $!\n";
END { rmdir $dir if $dir; }


sub stash_environ () {
    my $environment = new FileHandle "$dir/environment", 'w' or die $!;
    local ($,, $\) = ("\t", "\n");
    while (my ($key, $value) = each %ENV) {
	$environment->print($key, $value)
	    if $key =~ /^HTTP_SAMPLER_/;
    }
    $environment->print('DATE', strftime('%F %T', gmtime));
}


sub stash_file ($) {
    my $name = shift;
    $name =~ /^([-_0-9A-Za-z]+)$/ or die "uploaded file name looks suspicious: $name\n";
    my $filename = $1;
    my $in = $query->upload($name) or die "cannot read uploaded file: $!\n";
    my $out = new FileHandle "$dir/$filename.gz", 'w' or die "cannot stash uploaded file: $!\n";
    copy($in, $out);
}


stash_environ();
stash_file $_ foreach $query->param;


########################################################################


use lib '/usr/lib/perl5/site_perl/5.6.1';
use DB_File::Lock;
use POSIX qw(S_IRUSR S_IWUSR);


my $remote = $query->remote_host;

my %survey;
tie %survey, 'DB_File::Lock', '/var/www/sampler-survey/distro.db', O_RDONLY, 0, $DB_HASH, 'read';
my $novel = not exists $survey{$remote};
untie %survey;

if ($novel) {
    my $pollster = 'http://www.cs.berkeley.edu/~liblit/sampler/survey/';
    
    print($query->start_html('Distribution Change Survey'),
	  $query->h1('Distribution Change Survey'),
	  $query->p('Thank you for participating in the Cooperative Bug Isolation Project.  We are considering changing our CBI build platform and would like your input.  Please',
		    $query->a({href => $pollster}, 'visit the survey page'),
		    'and let us know what you think.'),
	  $query->p("(If that hyperlink does not work, use your bowser to go to &lt;$pollster&gt; manually.)"),
	  $query->end_html);
}
