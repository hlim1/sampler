#!/usr/bin/perl -w

use strict;
use File::Basename;
use File::Find;
use File::Path;
use File::stat;
use FileHandle;
use FindBin;

my $buildRoot = shift;
die "Usage: $0 <rpm-build-root>\n" unless defined $buildRoot;

my $buildDir = $ENV{RPM_BUILD_DIR};
die "no \$RPM_BUILD_DIR in environment\n" unless defined $buildDir;

my $extract = "$FindBin::Bin/extract-section";


sub wanted {
    return unless -f $_;

    my $stat = stat $_;
    my $mode = $stat->mode;
    return unless $mode & 0100 || $mode & 0010 || $mode & 0001;

    my $exe = new FileHandle;
    $ENV{PATH} = '/usr/bin:/bin';
    open $exe, '-|', 'file', $_;
    my $magic = $exe->getline;
    return unless $magic =~ /:[ \t]*ELF.*, not stripped/;
    die unless $exe->close;

    my $subpath = $_;
    die "$subpath is not under $buildRoot" unless $subpath =~ s{^\Q$buildRoot\E/}{};

    print "extracting sampler site info from $_\n";
    my $output = "$buildRoot/usr/lib/sampler/sites/$subpath";
    my $outdir = dirname $output;
    mkpath $outdir;

    my $sites = new FileHandle "$output.sites", 'w';
    $exe = new FileHandle;
    open $exe, '-|', $extract, '.debug_site_info', $File::Find::name;
    while (<$exe>) {
	s{^\Q$buildDir\E/}{/usr/src/debug/};
	$sites->print($_);
    }
    die unless $exe->close;

    my $cfg = new FileHandle "$output.cfg", 'w';
    $exe = new FileHandle;
    open $exe, '-|', $extract, '.debug_sampler_cfg', $File::Find::name;
    while (<$exe>) {
	s{(^|\t)\Q$buildDir\E/}{$1/usr/src/debug/};
	$cfg->print($_);
    }
    die unless $exe->close
}


find {wanted => \&wanted, no_chdir => 1}, $buildRoot;
